cmake_minimum_required(VERSION 3.19)

project(quicr
  VERSION 0.1
  LANGUAGES C CXX
)

option(CLANG_TIDY "Perform linting with clang-tidy" OFF)

###
### Global Config
###
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

include(CheckCXXCompilerFlag)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
if (CMAKE_CXX_COMPILER_ID MATCHES "Clang" OR CMAKE_CXX_COMPILER_ID MATCHES "GNU")
  add_compile_options(-pedantic -Wmissing-declarations)
elseif(MSVC)
  add_compile_options(/W4 /WX)

  # MSVC helpfully recommends safer equivalents for things like
  # getenv, but they are not portable.
  add_definitions(-D_CRT_SECURE_NO_WARNINGS)  
endif()

if(CLANG_TIDY)
  find_program(CLANG_TIDY_EXE NAMES "clang-tidy")
  if(CLANG_TIDY_EXE)
    set(CMAKE_CXX_CLANG_TIDY  ${CLANG_TIDY_EXE})
  else()
    message(WARNING "clang-tidy requested, but not found")
  endif()
endif()

###
### Enable testing
###
enable_testing()

###
### Dependencies
###
# External libraries
find_package(OpenSSL 1.1 REQUIRED)


# contrib
option(GIT_SUBMODULE "Check submodules during build" ON)
include(cmake/AddGitSubmodule.cmake)
add_git_submodule(${CMAKE_SOURCE_DIR}/contrib/picotls)
add_git_submodule(${CMAKE_SOURCE_DIR}/contrib/picoquic)
add_git_submodule(${CMAKE_SOURCE_DIR}/contrib/qproto)

set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
find_package(PTLS)
find_package(Picoquic)
find_package(QProto REQUIRED)

message (STATUS "Header: ${QPROTO_INCLUDE_DIRS}")
message (STATUS "Header: ${Picoquic_INCLUDE_DIRS}")
message (STATUS "Header: ${PTLS_INCLUDE_DIRS}")

include_directories(include tests
        ${QPROTO_INCLUDE_DIRS}
        ${Picoquic_INCLUDE_DIRS} 
        ${PTLS_INCLUDE_DIRS} 
        ${OPENSSL_INCLUDE_DIR})


if (OPENSSL_FOUND)
  include_directories(${OPENSSL_INCLUDE_DIR})
  set (SSL_LIBS ${OPENSSL_LIBRARIES})
else()
  set(SSL_LIBS )
endif()


###
### Library Config
###

set(LIB_NAME "${PROJECT_NAME}")

file(GLOB_RECURSE LIB_HEADERS CONFIGURE_DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/include/*.h")
file(GLOB_RECURSE LIB_SOURCES CONFIGURE_DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp")

add_library(${LIB_NAME} ${ILB_HEADERS} ${LIB_SOURCES})
target_link_directories(${LIB_NAME} PUBLIC ${Picotls_LIB_DIR} ${Picoquic_LIB_DIR} ${QProto_LIB_DIR})
target_link_libraries(${LIB_NAME} OpenSSL::Crypto)
target_include_directories(${LIB_NAME}
  PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include/${PROJECT_NAME}-${PROJECT_VERSION}>
  PRIVATE
    ${OPENSSL_INCLUDE_DIR}
)

target_link_libraries(${LIB_NAME} ${QPROTO_LIBRARIES} ${Picoquic_LIBRARIES} ${PTLS_LIBRARIES} ${SSL_LIBS})

###
### Tests
###
add_subdirectory(test)

###
### Applications
###

add_subdirectory(cmd)
